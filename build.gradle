apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

apply from: 'gradle-api-source.gradle'
apply from: 'publish-maven.gradle'

repositories {
    mavenCentral()
}

group = 'io.spring.gradle'
description = 'Dependency Management Plugin'

ext {
    spockVersion = GroovySystem.version.startsWith('1.') ? '0.7-groovy-1.8' : '0.7-groovy-2.0'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy'
    }
}

tasks.findByPath('artifactoryPublish')?.configure {
    properties = [
            'gradle-plugin'  : "io.spring.dependency-management:${project.group}:${project.name}",
            'bintray.package': "${project.group}:dependency-management",
            'bintray.version': "${project.version}"
    ]
}

groovydoc {
    docTitle = "$project.description $project.version API"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from groovydoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
    distributionUrl = 'http://services.gradle.org/distributions/gradle-1.12-all.zip'
}